##
## File            : $File: (normalize_sources.magik) $
## SCM             : $URL$
##
## Copyright       : Realworld Systems
##                   e-mail  : support@realworld-systems.com
##                   address : Venusstraat 17, 4105 JH Culemborg, The Netherlands
##                   tel     : +31(0)345 614406
##                   fax     : +31(0)345 614319
##
## Contains        : normalizes the sources in the
## method_finder, replacing the absolute paths by ones with an
## environment variable
##
##
## Date written    : 2011-07-20
## Date changed    : $Date$
## Revision        : $Rev$

_package user
$

_iter _method method_finder.roos_directories()
  l_dirs << rope.new()

  method_finder.npr("list_directories")

  (l_count_paths, dummy) << .input.get_line()

  _for i _over 1.upto(l_count_paths.as_number())
  _loop
    (i_dir, dummy) << .input.get_line()
    _loopbody(i_dir)
  _endloop
_endmethod

_method method_finder.roos_change_directory(p_old_directory, p_new_directory)
  method_finder.npr("change_directory " + p_old_directory + " " + p_new_directory)
_endmethod


_global normalize_sources <<
_proc @ normalize_sources(mi)

  _local l_t << date_time.now()

  l_orig_path_prefix << system.canonicalise(system.getenv("PROJECT_DIR"))
  l_new_path_prefix << "$PROJECT_DIR"

  # populate list with our absolute paths
  l_absolute_paths << rope.new()
  _for i_path _over method_finder.roos_directories()
  _loop
    _if i_path.index_of_seq(l_orig_path_prefix) = 1
    _then
      l_absolute_paths.add(i_path)
    _endif
  _endloop

  # change them all
  _for i_old_path _over l_absolute_paths.fast_elements()
  _loop
    i_new_path << l_new_path_prefix + i_old_path.slice_to_end(l_orig_path_prefix.size + 1)
    method_finder.roos_change_directory(i_old_path, i_new_path)
  _endloop

  write("**** Info: normalized source paths[", l_absolute_paths.size, "] (", date_time.now() - l_t, ")")

_endproc
$
